buildscript {
    apply from: "$project.rootDir/gradle/shared.gradle"

    dependencies {
        classpath "digital.wup:android-maven-publish:${MAVEN_PUBLISH_VERSION}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${DOKKA_VERSION}"
    }
}

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    // Extension of maven plugin that properly handles android dependencies
    id 'digital.wup.android-maven-publish' version "${MAVEN_PUBLISH_VERSION}"
    id 'org.jetbrains.dokka' version "${DOKKA_VERSION}" // Official Kotlin documentation engine - does both Kotlin and Java docs
    id 'org.jetbrains.kotlin.plugin.serialization' version "${kotlin_version}"
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

android {
    compileSdk 34

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro'
        buildConfigField("String", "VERSION_NAME", "\"${project.LIBRARY_VERSION}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
        }
    }

    resourcePrefix 'arm'
    buildFeatures {
        buildConfig true
    }
    namespace 'com.scribd.armadillo'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'

    implementation "com.google.android.exoplayer:exoplayer-core:${EXOPLAYER_VERSION}"
    implementation "com.google.android.exoplayer:exoplayer-hls:${EXOPLAYER_VERSION}"
    implementation "com.google.android.exoplayer:exoplayer-dash:${EXOPLAYER_VERSION}"
    implementation "com.google.android.exoplayer:extension-mediasession:${EXOPLAYER_VERSION}"

    implementation "io.reactivex.rxjava2:rxjava:${RXJAVA_VERSION}"
    implementation "io.reactivex.rxjava2:rxandroid:${RXANDROID_VERSION}"
    implementation "com.google.dagger:dagger:${DAGGER_VERSION}"
    kapt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    implementation 'androidx.media:media:1.6.0'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:${SERIALIZATON_VERSION}"

    testImplementation "org.robolectric:robolectric:4.9.2"
    testImplementation 'junit:junit:4.13.2'
    testImplementation("org.assertj:assertj-core:3.10.0")
    testImplementation "org.mockito:mockito-core:5.6.0"
    testImplementation("org.mockito.kotlin:mockito-kotlin:4.1.0") {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect'
    }
}

task androidSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

// Let dokka do the javadoc creation, then pick up its output as our artifact
task androidJavadocsJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

// Configure AAR to publish
publishing {

    publications {
        android.libraryVariants.all { variant ->
            "${variant.name.capitalize()}Aar"(MavenPublication) {
                from(components[variant.name])
                groupId project.PACKAGE_NAME
                version project.LIBRARY_VERSION
                artifactId project.getName().toLowerCase()
                // Add sources to artifact
                artifact androidSourcesJar
                // Add javadocs
                artifact androidJavadocsJar
            }
        }

        android.libraryVariants.all { variant ->
            "${variant.name.capitalize()}SnapshotAar"(MavenPublication) {
                from(components[variant.name])
                groupId project.PACKAGE_NAME
                version "${project.LIBRARY_VERSION}-SNAPSHOT"
                artifactId project.getName().toLowerCase()
                // Add sources to artifact
                artifact androidSourcesJar
                // Add javadocs
                artifact androidJavadocsJar
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/$githubRepo")
            credentials {
                username = "$githubUsername"
                password = "$githubPassword"
            }
        }
    }
}